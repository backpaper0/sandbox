buildscript {
    ext {
        kotlinVersion = '1.0.1-2'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

ext {
    javaeeVersion = '7.0'
    payaraVersion = '4.1.1.161.1'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'eclipse'

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compileOnly "javax:javaee-api:${javaeeVersion}"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    archives "fish.payara.extras:payara-micro:${payaraVersion}"
}

war.archiveName = 'sample.war'

task run(type:Exec) {
    def payaraJar = configurations.archives.find { it.name == "payara-micro-${payaraVersion}.jar" }
    def warFile = war.archivePath
    commandLine 'java', '-Ddb.url=jdbc:h2:file:./build/data', '-jar', "${payaraJar}", '--noCluster', '--deploy', "${warFile}"
}

run.dependsOn war
