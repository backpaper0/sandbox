apply plugin: 'java'
apply plugin: 'eclipse'

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

compileJava.dependsOn processResources
processResources.destinationDir = compileJava.destinationDir

repositories {
    mavenCentral()
}

configurations {
    domaGen
}

dependencies {
    compile 'com.h2database:h2:1.4.196'
    compile 'org.seasar.doma:doma:2.16.1'
    domaGen 'org.seasar.doma:doma-gen:2.16.1'
    testCompile 'junit:junit:4.12'
}

task gen {
    doLast {
        ant.taskdef(
                resource: 'domagentask.properties',
                classpath: sourceSets.main.output.classesDir.toString() + File.pathSeparator + (configurations.domaGen + configurations.runtime).asPath)
        ant.gen(url: "jdbc:h2:mem:sample;INIT=RUNSCRIPT FROM 'sample.sql'", user: 'sa', password: 'secret', dialectName: 'h2') {
            entityConfig(
                    destDir: 'src/main/java',
                    packageName: 'com.example.entity',
                    superclassName: 'com.example.EntitySkeleton',
                    listenerSuperclassName: 'com.example.EntityListenerSkeleton',
                    useAccessor: false)
            daoConfig(generate: false)
            sqlConfig(generate: false)
        }
    }
}

