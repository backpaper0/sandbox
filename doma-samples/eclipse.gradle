apply plugin: 'eclipse'

eclipse.jdt.file {
    beforeMerged {
        file('.settings/org.eclipse.jdt.apt.core.prefs').withWriter('UTF-8') { out ->
            def props = [
                'eclipse.preferences.version'         : '1'
               ,'org.eclipse.jdt.apt.aptEnabled'      : 'true'
               ,'org.eclipse.jdt.apt.genSrcDir'       : '.apt_generated'
               ,'org.eclipse.jdt.apt.reconcileEnabled': 'true'
            ] as Properties
            props.store(out, '')
        }
        file('.factorypath').withWriter('UTF-8') { out ->
            new groovy.xml.MarkupBuilder(out).factorypath {
                factorypathentry(
                    kind: 'EXTJAR',
                    id: configurations.compile.find { it.name == "doma-${domaVersion}.jar" }.absolutePath,
                    enabled: true,
                    runInBatchMode: false)
            }
        }
    }
    withProperties { props ->
        props['org.eclipse.jdt.core.compiler.processAnnotations'] = 'enabled'
    }
}
